prototype(Jonnitto.Plyr:Video) < prototype(Jonnitto.Plyr:Tag) {
    playsinline = ${Configuration.setting('Jonnitto.Plyr.options.playsinline')}
    poster = ${q(node).property('image')}
    media = Neos.Fusion:Join {
        webm = Jonnitto.Plyr:FileCollection {
            fileextension = 'webm'
        }
        ogg = Jonnitto.Plyr:FileCollection {
            fileextension = 'ogg'
        }
        mp4 = Jonnitto.Plyr:FileCollection {
            fileextension = 'mp4'
        }

        // Captions: Title as Label Captions as language
        vtt = Jonnitto.Plyr:FileCollection {
            fileextension = 'vtt'
            itemRenderer {
                @process.wrap = ${'<track kind="captions" label="' + asset.title + '" src="' + value + '" srclang="'+ asset.caption + (iteration.isFirst ? ' default' : '') + '">'}
            }
        }
    }

    content {
        tagName = 'video'
        attributes {
            playsinline = ${props.playsinline}
            controls = true
            preload = ${props.poster ? 'none' : true}
            poster = Neos.Neos:ImageUri {
                asset = ${props.poster}
                maximumWidth = 1920
                @if.hasPicture = ${props.poster ? true : false}
            }
        }
        content = ${props.media}
    }
}
