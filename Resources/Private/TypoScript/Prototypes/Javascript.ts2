prototype(Plyr:Javascript) < prototype(Fusion:Array) {
	@context.settings = ${Configuration.setting('Jonnitto.Plyr')}
	library = Fusion:Tag {
		tagName = 'script'
		attributes {
			async = true
			src = Fusion:ResourceUri {
				path = 'resource://Jonnitto.Plyr/Public/Main.js'
			}
		}
		@if.notInBackend = ${node.context.live}
	}
	call = Fusion:Tag {
		tagName = 'script'
		content = Fusion:Array {
			options = Fusion:Template {
				templatePath = 'resource://Jonnitto.Plyr/Private/Templates/Javascript.js'
				options = ${settings.options}
				controls = Fusion:Case {
					isDefault {
						condition = ${settings.options.controls == ['play-large','play','progress','current-time','mute','volume','captions','fullscreen'] ? true : false}
						renderer = false
					}
					default {
						condition = true
						renderer = ${"['" + Array.join(settings.options.controls, "','") + "']"}
					}
				}
				i18n = ${['restart','rewind','play','pause','forward','buffered','currentTime','duration','volume','toggleMute','toggleCaptions','toggleFullscreen']}
				@process.clean = ${String.pregReplace(String.pregReplace(String.pregReplace(String.pregReplace(String.pregReplace(value, '/\/\/ jshint ignore:start\n/', ''), '/[\s]+/', ' '), '/[\s]+({|}|=|\+|;)[\s]+/', '$1'), '/(\',|;)[\s]+/', '$1'), '/;}/', '}')}
			}
			backend = Fusion:Template {
				templatePath = 'resource://Jonnitto.Plyr/Private/Templates/Backend.js'
				@if.inBackend = ${node.context.inBackend}
			}
		}
	}

	@position = 'end'
	@if {
		includeJS = ${settings.includeJavascript}
		includeCase = Plyr:IncludeCase
	}
}
